LABTITLE=: 'FVJ4 dwin_ja.ijs (jandroid) Simple object drawing windows'
LABAUTHOR=: 0 : 0
Cliff Reiter
Math Dept
Lafayette College
Easton, PA 18042 USA
reiterc@lafayette.edu
)
LABFOCUS=: 1

NB. =========================================================
Lab Section Object window script
This lab offers an overview of some of the facilities in the script dwin.ijs associated with the 4th edition of the book Fractals, Visualization and J. These object windows require the JQT/JAndroid interface. They are primitive but are sufficient for experimenting with polygons and transformations. 
)
load '~addons/graphics/fvj4/dwin_ja.ijs'

NB. =========================================================
Lab Section First plots
First we open a window, then we draw a green square and a red diagonal line.
)
_1 _1 1 1 dwin 'Object window' ; 0 : 0
]sq__=:#:0 1 3 2
0 255 0 dpoly sq__
255 0 0 dline _1 1,:1 _1
)

NB. =========================================================
Lab Section Smaller, asymetric window
Now we repeat the experiment with a smaller, asymmetric window.
)
setWIN_WH 600 300
_1 _1 1 1 dwin 'Object window' ; 0 : 0
0 255 0 dpoly sq__
255 0 0 dline _1 1,:1 _1
)

NB. =========================================================
Lab Section Random polygons
Next we plot 200 random quadralerals. You may close the graphics window before proceeding.
)
wd'reset;'
setWIN_WH 500 500
0 0 100 100 dwin '200 random quadralaterals' ; 0 : 0
(?200 3$256) dpoly ?200 4 2$100
)

NB. =========================================================
Lab Section Animation
We can open animation windows with dawin. Here we will
use the animation window to rotate a random polygon. You may close the animation window before proceeding.
)
wd'reset;'
rp__=:0.007*?. 8 2$100
_1 _1 1 1 0.1 dawin 'animated rotation' ; 0 : 0
rot=:(cos,sin),:-@sin,cos
255 0 0 dapoly rp__ (+/ . *)"_ 2 rot"0] 2p1*(i.%<:) 100
)

NB. =========================================================
Lab Section 3-D Polygon
We can plot this polygon in 3-D. You may close the 3D window before proceeding.
)
wd'reset;'
rp3__=:rp__,.0
_1 _1 _1 1 1 1 d3win '3D window' ; 0 : 0
255 0 0 d3poly rp3__
)

NB. =========================================================
Lab Section Animation of 3-D Polygon
Lastly, we can animate it in 3D using the built-in rotz; however, we need to use homogeneous coordinates since rotz does. Try experimenting with rotations about other axes.
)
wd'reset'
_1 _1 _1 1 1 1 0.1 d3awin '3D animated rotation' ; 0 : 0
255 0 0 d3apoly (rp3__,0 0 0 1) (+/ . *)"_ 2 rotz"0] 2p1*(i.%<:) 100
)

